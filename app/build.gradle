apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def code_major = (major as int) * 10000
def code_minor = (minor as int) * 100
def code_patch = patch as int

android {

    defaultConfig {
        applicationId package_name
        minSdkVersion min_sdk_version as int
        targetSdkVersion target_sdk_version as int
        versionCode code_major + code_minor + code_patch
        versionName "${major}.${minor}.${patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Room Database Name
        buildConfigField 'String', "$key_room_database_name", "\"$room_database_name\""
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', "$key_base_url", "\"$release_base_url\""
            buildConfigField 'String', "$key_basic_user_name", "\"$release_user_name\""
            buildConfigField 'String', "$key_basic_pass", "\"$release_password\""
        }

        debug {
            buildConfigField 'String', "$key_base_url", "\"$debug_base_url\""
            buildConfigField 'String', "$key_basic_user_name", "\"$debug_user_name\""
            buildConfigField 'String', "$key_basic_pass", "\"$debug_password\""
        }

        local {
            debuggable true
            initWith debug

            buildConfigField 'String', "$key_base_url", "\"$local_base_url\""
            buildConfigField 'String', "$key_basic_user_name", "\"$local_user_name\""
            buildConfigField 'String', "$key_basic_pass", "\"$local_password\""
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    dataBinding {
        enabled = true
    }

    compileSdkVersion compile_sdk_version as int
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support Library
    //noinspection GradleCompatible
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:support-v4:$support_lib_version"

    // Architecture Component
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    // Dependency Injection
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Reactive X
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    // API Client
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"

    // Json Parser
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Debug Log
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Test Library
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.android.support.test:runner:$test_runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
}

kapt {
    generateStubs = true
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}
