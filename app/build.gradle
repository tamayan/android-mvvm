apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getLocalProperties(String key) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty(key)
}

android {

    defaultConfig {
        applicationId 'com.example.androidmvvm'
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName '1.0.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // Room Database Name
        buildConfigField 'String', "$key_room_database_name", "\"$room_database_name\""
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', "$key_base_url", "\"${getLocalProperties('release_base_url')}\""
            buildConfigField 'String', "$key_basic_user_name", "\"${getLocalProperties('release_base_url')}\""
            buildConfigField 'String', "$key_basic_pass", "\"${getLocalProperties('release_base_url')}\""
        }

        debug {
            buildConfigField 'String', "$key_base_url", "\"${getLocalProperties('debug_base_url')}\""
            buildConfigField 'String', "$key_basic_user_name", "\"${getLocalProperties('debug_user_name')}\""
            buildConfigField 'String', "$key_basic_pass", "\"${getLocalProperties('debug_password')}\""
        }

        local {
            initWith debug
            debuggable true

            buildConfigField 'String', "$key_base_url", "\"${getLocalProperties('local_base_url')}\""
            buildConfigField 'String', "$key_basic_user_name", "\"${getLocalProperties('local_user_name')}\""
            buildConfigField 'String', "$key_basic_pass", "\"${getLocalProperties('local_password')}\""
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    dataBinding {
        enabled = true
    }

    compileSdkVersion 30
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '30.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"

    // Support Library
    //noinspection GradleCompatible
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Architecture Component
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // Dependency Injection
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-android-scope:2.0.1'
    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    // Reactive X
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    // API Client
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'

    // Json Parser
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.5.0'

    // Debug Log
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // Test Library
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.koin:koin-test:0.9.3'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

kapt {
    generateStubs = true
}

repositories {
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
}
